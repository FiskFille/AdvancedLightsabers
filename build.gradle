buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = "1.2.2"
group = "com.fiskmods.lightsabers"
archivesBaseName = "AdvancedLightsabers-1.7.10"

sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "eclipse"
    replace '${version}', '1.2.2'
}

sourceSets {
    main {
        resources {
            srcDir 'resources'
        }
    }
}

def coremodManifest = {
    attributes 'FMLCorePlugin': 'com.fiskmods.lightsabers.asm.ALLoadingPlugin'
    attributes 'FMLCorePluginContainsFMLMod': 'true'
	attributes 'Main-Class': 'com.fiskmods.lightsabers.saberbuilder.SaberBuilder'
	attributes 'Class-Path': '.'
}

jar {
    destinationDir = file 'jars'
    includeEmptyDirs = false
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	
    manifest coremodManifest
}

dependencies {
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task deobfJar(type: Jar) {
	destinationDir = file 'jars'
    classifier = 'deobf'
	
	from sourceSets.main.output
	manifest coremodManifest
}

task sourceJar(type: Jar, dependsOn:classes) {
	destinationDir = file 'jars'
    classifier = 'src'
	
    from sourceSets.main.allJava
}

artifacts {
    archives deobfJar
    archives sourceJar
}
